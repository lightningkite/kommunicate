buildscript {
    ext.kotlinVersion = '1.3.20'
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}
apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    pkg {
        repo = project.group.toString()
        name = project.name
        userOrg = "lightningkite"
        licenses = ["MIT"]
        def github = "https://github.com/lightningkite/${project.name}"
        vcsUrl = github
        websiteUrl = github
        issueTrackerUrl = github + "/issues"
        version {
            name = project.version
            released = new Date()
        }
    }
}


repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}
group 'com.lightningkite'
version '0.0.2'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')
        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            fromPreset(presets.iosX64, 'iosx64')
            fromPreset(presets.iosArm64, 'ios')
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'

                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.1.0'

            }
        }
        commonTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-common'
                api 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'


                api 'com.squareup.okhttp3:okhttp:3.11.0'
            }
        }
        jvmTest {
            dependencies {
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
                api 'org.jetbrains.kotlin:kotlin-test'
                api 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'

            }
        }
        jsTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-js'
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.1.0'
            }
        }

        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            iosMain {
                dependencies {
                }
            }
            iosTest {
                dependencies {
                    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.0'
                }
            }
            iosx64Main {
                dependencies {
                    kotlin.srcDir('src/iosMain/kotlin')
                }
            }
            iosx64Test {
                dependencies {
                    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.1.0'
                    kotlin.srcDir('src/iosTest/kotlin')
                }
            }
        }
    }
}
tasks.withType(compileKotlinJs.getClass()) {
    kotlinOptions {
        moduleKind = "umd"
        sourceMap = true
        metaInfo = true
    }
}

if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    task iosTest {
        def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
        dependsOn 'linkTestDebugExecutableIosx64'
        group = JavaBasePlugin.VERIFICATION_GROUP
        description = "Runs tests for target 'ios' on an iOS simulator"

        doLast {
            def binary = kotlin.targets.iosx64.compilations.test.getBinary('EXECUTABLE', 'DEBUG')
            exec {
                commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
            }
        }
    }
    tasks.check.dependsOn(tasks.iosTest)
}
